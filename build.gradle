plugins {
	id 'com.github.johnrengelman.shadow' version '5.0.0'
}
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'eclipse-wtp'
apply plugin: 'java'

group = 'ca.umanitoba.dam.rdfhashing'

repositories {
    mavenCentral()
    mavenLocal()
}

// Java 8 only
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
	buildName = "java-rdf-hashing"
    versions = [
    		commonsCli : '1.4',
    		commonsIo : '1.3.2',
            jenaRdf : '0.5.0',
            logback : '1.2.3',
            junit : '5.3.0'
    ]
}

dependencies {
	compile group: 'commons-cli', name: 'commons-cli', version: versions.commonsCli
    compile group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
    compile group: 'org.apache.commons', name: 'commons-rdf-jena', version: versions.jenaRdf
    
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
	testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
	testRuntime group: 'org.apache.commons', name: 'commons-io', version: versions.commonsIo
}

// Make a sources jar (if you want it)
task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    archiveBaseName = buildName
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
    archiveBaseName = buildName
}

// Make our JAR file
jar {
    manifest {
    		attributes 'Implementation-Title': buildName,
    			'Implementation-Version': project.version,
    			'Description': project.description
    }
    archiveBaseName = buildName
    version = project.version
}

// Executable jar with all dependencies.
task fatJar(type: Jar) {
	group = BasePlugin.BUILD_GROUP
	mustRunAfter "jar"
	description = 'Create executable jar with all dependencies'
    manifest {
        attributes 'Implementation-Title': buildName,
    			'Implementation-Version': project.version,
    			'Main-Class': project.group + '.HashCli'
    }
    archiveName = buildName + "-" + project.version + "-all.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

shadowJar {
   baseName = buildName
   classifier = "all"
   version = project.version
   manifest {
        attributes 'Implementation-Title': buildName,
    			'Implementation-Version': project.version,
    			'Description': project.description,
    			'Main-Class': project.group + '.HashCli'
    }
}

artifacts {
    archives javadocJar
    archives sourceJar
    archives shadowJar
}

test {
	// Requires gradle 4.6 or greater
	useJUnitPlatform()
    testLogging {
        // Make sure output from
        // standard out or error is shown
        // in Gradle output.
        // showStandardStreams = true
        //events 'standard_out', 'standard_error'
        events "passed", "skipped", "failed"
        
        exceptionFormat = 'full'
    }
}

